import org.ajoberstar.grgit.Person

plugins {
  id 'com.avast.gradle.docker-compose' version '0.5.1'
  id 'org.ajoberstar.grgit' version '2.0.0'
}

defaultTasks 'composeDown', 'clean', 'prepFixtures', 'build', ':web:grunt_default', 'composeUp'

composeUp.dependsOn ':java:war'

task prepFixtures {
  description 'Prepare development environment with test fixtures'
  doFirst {
    delete {
      delete fileTree(dir: 'mapfiles')
      delete 'datastore/.git'
      delete fileTree(dir: 'datastore')
    }
    copy {
      into 'datastore'
      from 'fixtures/datastore/REV-1'
    }
    def repo = grgit.init(dir: 'datastore')
    repo.add(patterns: ['.'])
    repo.commit(message: 'Adding fixtures', committer: new Person('prep', 'prep@example.com'))
  }
}

task unzipWeb(type: Exec, dependsOn: ':java:build') {
  commandLine 'docker-compose', 'exec', '-T', 'web', 'unzip', '-od', '/usr/local/tomcat/webapps/ROOT', '/opt/ceh-catalogue/libs/ROOT.war'
}

task web(type: Exec, dependsOn: unzipWeb) {
  inputs.files(fileTree("java/src/main/java"))
  description 'Builds the java files, unzips ROOT.war then restarts service'
  commandLine 'docker-compose', 'restart', 'web'
}

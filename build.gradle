import org.apache.tools.ant.DirectoryScanner
import org.ajoberstar.grgit.Person

plugins {
  id 'com.avast.gradle.docker-compose' version '0.5.1'
  id 'org.ajoberstar.grgit' version '2.0.0'
}

defaultTasks 'composeDown', 'clean', 'prepFixtures', 'build', ':web:grunt_default', 'composeUp'

composeUp.dependsOn ':java:war'

task prepFixtures {
  description 'Prepare development environment with test fixtures'
  doFirst {
    delete {
      delete fileTree(dir: 'mapfiles')
      delete 'datastore/.git'
      delete fileTree(dir: 'datastore')
    }
    copy {
      into 'datastore'
      from 'fixtures/datastore/REV-1'
    }
    def repo = grgit.init(dir: 'datastore')
    repo.add(patterns: ['.'])
    repo.commit(message: 'Adding fixtures', committer: new Person('prep', 'prep@example.com'))
  }
}

if (System.getenv("SELENIUM_TEST") ?: false) {
  dockerCompose {
    useComposeFiles = ['docker-compose.yml', 'docker-compose.selenium.yml']
    startedServices = ['chrome', 'firefox']
  }
}

// Run as:
// SELENIUM_TEST=true ./gradlew selenium
task selenium(type: Exec, dependsOn: prepFixtures) {
  commandLine 'docker-compose', '--file=docker-compose.yml', '--file=docker-compose.selenium.yml', 'run', 'ruby_test'
}

dockerCompose.isRequiredBy(selenium)

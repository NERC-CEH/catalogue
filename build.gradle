import org.apache.tools.ant.DirectoryScanner

plugins {
  id 'com.avast.gradle.docker-compose' version '0.5.1'
}

defaultTasks 'composeDown', 'clean', 'copyDatastore', 'build', ':web:grunt_default', 'composeUp'

composeUp.dependsOn ':java:war'

task deleteFixtures {
  description 'Clear development environment of test fixtures'

  doFirst {
    delete {
      delete fileTree(dir: 'mapfiles')
      delete 'datastore/.git'
      delete fileTree(dir: 'datastore')
    }
  }
}

task copyDatastore(type: Copy) {
  doFirst {
    // Ant excludes .git by default!
    // see https://issues.gradle.org/browse/GRADLE-1883?focusedCommentId=19276&page=com.atlassian.jira.plugin.system.issuetabpanels:comment-tabpanel#comment-19276
    DirectoryScanner.defaultExcludes.each { DirectoryScanner.removeDefaultExclude it }
    DirectoryScanner.addDefaultExclude 'something has to be in here or everything gets excluded'
  }
  from 'fixtures/datastore/REV-1'
  into 'datastore'
  dependsOn 'deleteFixtures'
}

if (System.getenv("SELENIUM_TEST") ?: false) {
  dockerCompose {
    useComposeFiles = ['docker-compose.yml', 'docker-compose.selenium.yml']
    startedServices = ['chrome', 'firefox']
  }
}

// Run as:
// SELENIUM_TEST=true ./gradlew selenium
task selenium(type: Exec) {
  commandLine 'docker-compose', '--file=docker-compose.yml', '--file=docker-compose.selenium.yml', 'run', 'ruby_test'
}

dockerCompose.isRequiredBy(selenium)
